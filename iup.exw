--------------------------------------------------------------------------------
-- iup.exw
--------------------------------------------------------------------------------
-- Version 4.0.5.2
-- Date: 2022.01.26
-- Author: C A Newbould
-- Status: operational
-- Changes:
--* modified to reflect name changes in //iup.ew//
--------------------------------------------------------------------------------
-- This app tests a basic IUP session, with a top-level window, some widgets
-- inside some containers, with various attributes set.
--
-- The callback mechanism is also tested.
--------------------------------------------------------------------------------
-- Required files
--------------------------------------------------------------------------------
include library/iup.e -- for IUP engine
include library/iup.ew -- for IUP widgets
--------------------------------------------------------------------------------
-- The IUP app --
--* set up the standard IUP structure: Open; MainLoop; Close
--* use constants (invariants) to identify widgets and colours
--* qualify the basic design with layout and attribute settings
--* link the event handlers - they can appear anywhere in the module
--* define, title, size and display the top-level window
--
-- Special features:
--* use of html markup tags to emphasise certain textual items
--* use of 'New' and its ability to assign multiple attributes in one step
--* use of unnamed Constructors
--* use of gaps to space out items
--* use of colour
--* use of leading commas to emphasise continuation of definitions
--* use of different default actions in widget definitions
--* calling non-void functions as if they are procedures
--* use of the two different Alarm functions
--------------------------------------------------------------------------------
Open()
    constant b = Button("I can close the App")
            ,b1 = Button("I call a Dialog-based Alarm", "BUTTON_CB")
            ,b2 = Button("Use GetText")
            ,b3 = Button("I call the IUP Alarm function")
            ,lbl = Label("I am a Label")
            ,v = Vbox({b,b2,New(Frame(b3),"TITLE=\"I am a Frame\""),lbl})
            ,RED = "255 0 0", GREEN = "0 255 0"
    setAttribute(b, "FGCOLOR", RED)
    setCallback(b, "ACTION",Icallback("b_onClick"))
    setCallback(b1, "BUTTON_CB",Icallback("b1_onClick"))
    setCallback(b2, "ACTION", Icallback("b2_onClick"))
    setCallback(b3, "ACTION", Icallback("b3_onClick"))
    setAttribute(lbl, "FGCOLOR", GREEN)
    setAttribute(lbl, "TIP", "I do nothing")
    setAttribute(v, "GAP", "30")
    constant t = New(Toggle("I am a <b>Toggle</b>"), "MARKUP=YES,VALUE=ON")
            ,h = New(Hbox({v, b1, t}), "GAP=20")
            ,d = New(Dialog(h), "DIALOGFRAME=YES,SIZE=400x200,TITLE=Testing")
    show(d, {100, 50})
    mainLoop()
Close()
--------------------------------------------------------------------------------
-- Event handlers
--
--* arguments are not strictly necessary for these actions
--* all event-handlers must return a value - usually one of the standard values
--------------------------------------------------------------------------------
include library/string.e -- for 'string'
function b_onClick(Ihandle self)
    return CLOSE
end function
function b1_onClick(Ihandle self)
    integer i = alarm(d, getAttribute(d, "SIZE"),
                        "Testing's SIZE attribute", "OKCANCEL")
    string btn
    switch i do
    case 1 then btn = "OK"
    case 2 then btn = "Cancel"
    case 3 then btn = "Escape"    
    end switch
    Message("You clicked " & btn, "Alarming")
    return DEFAULT
end function
function b2_onClick(Ihandle self)
    sequence s = GetText("", "Title", 300)
    Message(s[2],"The text you keyed")
    return DEFAULT
end function
function b3_onClick(Ihandle self)
        integer i = Alarm("Message","Title","Button1","Button2")
        Message(sprintf("You chose Button #%d",i),"IUP Alarm")
    return DEFAULT
end function
--------------------------------------------------------------------------------
-- Previous versions
--------------------------------------------------------------------------------
-- Version 4.0.5.1
-- Date: 2022.01.25
-- Author: C A Newbould
-- Status: operational
-- Changes:
--* added test of IUP Alarm, via a third button
--* added a Frame container around this button
--------------------------------------------------------------------------------
-- Version 4.0.5.0
-- Date: 2022.01.01
-- Author: C A Newbould
-- Status: operational
-- Changes:
--* created
--* fully-documented
--------------------------------------------------------------------------------
