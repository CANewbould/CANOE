-- webbrowser.exw
--
-- Copyright (C) 2008-2010 by Jeremy Cowgar <jeremy@cowgar.com>
--
-- This file is part of EuIup.
--
-- EuIup is free software: you can redistribute it and/or modify
-- it under the terms of the GNU Lesser General Public License as
-- published by the Free Software Foundation, either version 3 of
-- the License, or (at your option) any later version.
--
-- EuIup is distributed in the hope that it will be useful,
-- but WITHOUT ANY WARRANTY; without even the implied warranty of
-- MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
-- GNU General Public License for more details.
--
-- You should have received a copy of the GNU Lesser General Public
-- License along with EuIup.  If not, see <http://www.gnu.org/licenses/>.
--

include library/iup.e as IUP
include library/iup.ew
include library/iupwb.ew as WB

Ihandle hDialog, hToolBar, hUrlEntry, hGoButton, hWebBrowser

function on_go(Ihandle self)
    SetAttribute(hWebBrowser, "VALUE", GetAttribute(hUrlEntry, "VALUE"))
    return DEFAULT
end function

function on_navigate_cb(Ihandle self)
    SetAttribute(hUrlEntry, "VALUE", GetAttribute(hWebBrowser, "VALUE"))
    return DEFAULT
end function

function on_completed_cb(Ihandle self)
    SetAttribute(hUrlEntry, "VALUE", "")--GetAttribute(hWebBrowser, "VALUE"))
    return DEFAULT
end function

procedure create_gui()
    hUrlEntry = New(Text(),"EXPAND=HORIZONTAL")
    hGoButton = Button("Go")
    SetCallback(hGoButton,"ACTION",Icallback("on_go"))
    hToolBar = Hbox({hUrlEntry, hGoButton})

    hWebBrowser = New(WebBrowser(), "EXPAND=YES")
    SetCallback(hWebBrowser, "NAVIGATE_CB", Icallback("on_navigate_cb"))
    SetCallback(hWebBrowser, "COMPLETED_CB", Icallback("on_completed_cb"))

    hDialog = New(Dialog(Vbox({hToolBar, hWebBrowser})), "MARGIN=5x5,GAP=5")
    hDialog = New(hDialog,"TITLE=\"Iup Web Browser\",SIZE=800x380")
end procedure

procedure main()
    IUP:Open()
    WB:Open()
    create_gui()
    Show(hDialog)
    -- Load our initial page
    SetAttribute(hWebBrowser, "VALUE", "http://openeuphoria.org")
    MainLoop()
    Close()
end procedure

main()
